# vim:fileencoding=utf-8:ft=zsh:foldmethod=marker
# Optimized zshrc for fast startup (~20ms target)
# Structure: Zim → Homebrew → Autoload → Modular configs → Environment

# Zim Plugin Manager {{{
# Configure Zim before initialization
ZSH_AUTOSUGGEST_MANUAL_REBIND=1  # Disable auto-rebind (last module optimization)
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)  # Limit syntax highlighting scope

# Zim paths
ZIM_HOME=${ZDOTDIR:-${HOME}}/.zim

# Download zimfw plugin manager if missing
if [[ ! -e ${ZIM_HOME}/zimfw.zsh ]]; then
  if (( ${+commands[curl]} )); then
    curl -fsSL --create-dirs -o ${ZIM_HOME}/zimfw.zsh \
        https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
  else
    mkdir -p ${ZIM_HOME} && wget -nv -O ${ZIM_HOME}/zimfw.zsh \
        https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
  fi
fi

# Install missing modules, and update ${ZIM_HOME}/init.zsh if missing or outdated
if [[ ! ${ZIM_HOME}/init.zsh -nt ${ZIM_CONFIG_FILE:-${ZDOTDIR:-${HOME}}/.zimrc} ]]; then
  source ${ZIM_HOME}/zimfw.zsh init
fi

# Initialize modules (handles history, completion, prompt, syntax highlighting, etc.)
source ${ZIM_HOME}/init.zsh

# zsh-history-substring-search key bindings
zmodload -F zsh/terminfo +p:terminfo
for key ('^[[A' '^P' ${terminfo[kcuu1]}) bindkey ${key} history-substring-search-up
for key ('^[[B' '^N' ${terminfo[kcud1]}) bindkey ${key} history-substring-search-down
for key ('k') bindkey -M vicmd ${key} history-substring-search-up
for key ('j') bindkey -M vicmd ${key} history-substring-search-down
unset key
# }}} End Zim configuration

# Homebrew Environment {{{
# Pre-expanded `brew shellenv` to avoid slow eval (~20ms saved)
if [ -f /proc/sys/kernel/osrelease ]; then
  # Linuxbrew
  export HOMEBREW_CELLAR="/home/linuxbrew/.linuxbrew/Cellar"
  export HOMEBREW_REPOSITORY="/home/linuxbrew/.linuxbrew/Homebrew"
  export PATH="/home/linuxbrew/.linuxbrew/bin:/home/linuxbrew/.linuxbrew/sbin${PATH+:$PATH}"
  [ -z "${MANPATH-}" ] || export MANPATH=":${MANPATH#:}"
  export INFOPATH="/home/linuxbrew/.linuxbrew/share/info:${INFOPATH:-}"
  [ -z "${XDG_DATA_DIRS-}" ] || export XDG_DATA_DIRS="/home/linuxbrew/.linuxbrew/share:${XDG_DATA_DIRS-}"
else
  # macOS Homebrew
  export HOMEBREW_PREFIX="/opt/homebrew"
  export HOMEBREW_CELLAR="/opt/homebrew/Cellar"
  export HOMEBREW_REPOSITORY="/opt/homebrew"
  export PATH="/opt/homebrew/bin:/opt/homebrew/sbin${PATH+:$PATH}"
  export MANPATH="/opt/homebrew/share/man${MANPATH+:$MANPATH}:"
  export INFOPATH="/opt/homebrew/share/info:${INFOPATH:-}"
fi
export HOMEBREW_NO_AUTO_UPDATE=1
# Fix compinit warning for old brew paths
fpath=(${fpath:#/usr/local/share/zsh/site-functions})
# }}} End Homebrew

# Autoload Functions {{{
# Lazy-load rarely-used functions for faster startup (~5ms saved)
# Functions are defined in ~/.zsh/functions/ and loaded on-demand
if [[ -d ~/.zsh/functions ]]; then
  fpath=(~/.zsh/functions $fpath)
  autoload -Uz ~/.zsh/functions/*(:t)
fi
# }}} End Autoload

# Modular Configuration {{{
# Load configs in numbered order (00-*, 10-*, etc.)
# This ensures PATH is set before other configs need it
ZSHRC_D="${ZDOTDIR:-$HOME}/.zshrc.d"
[ -d "$ZSHRC_D" ] || mkdir -p "$ZSHRC_D"
for rc in "$ZSHRC_D"/*.sh(N-.); do
  source "$rc"
done
unset rc
# }}} End Modular Config

# Environment Variables {{{
# Terminal settings
[[ -z "$TERM" || "$TERM" == "dumb" || "$TERM" == "xterm-ghostty" ]] && export TERM=xterm-256color
export LANG="en_US.UTF-8"
export LC_ALL="en_US.UTF-8"

# Default editor
export EDITOR="nvim"

# Prompt colors (for custom prompts)
export ON_COLOR=magenta
export OFF_COLOR=cyan

# Ripgrep config
export RIPGREP_CONFIG_PATH=$HOME/.ripgreprc

# }}} End Environment

# zerobrew
export ZEROBREW_DIR=/Users/pablopunk/.zerobrew
export ZEROBREW_BIN=/Users/pablopunk/.zerobrew/bin
export ZEROBREW_ROOT=/opt/zerobrew
export ZEROBREW_PREFIX=/opt/zerobrew
export PKG_CONFIG_PATH="$ZEROBREW_PREFIX/lib/pkgconfig:${PKG_CONFIG_PATH:-}"

# SSL/TLS certificates (only if ca-certificates is installed)
if [ -f "$ZEROBREW_PREFIX/opt/ca-certificates/share/ca-certificates/cacert.pem" ]; then
  export CURL_CA_BUNDLE="$ZEROBREW_PREFIX/opt/ca-certificates/share/ca-certificates/cacert.pem"
  export SSL_CERT_FILE="$ZEROBREW_PREFIX/opt/ca-certificates/share/ca-certificates/cacert.pem"
elif [ -f "$ZEROBREW_PREFIX/etc/ca-certificates/cacert.pem" ]; then
  export CURL_CA_BUNDLE="$ZEROBREW_PREFIX/etc/ca-certificates/cacert.pem"
  export SSL_CERT_FILE="$ZEROBREW_PREFIX/etc/ca-certificates/cacert.pem"
elif [ -f "$ZEROBREW_PREFIX/share/ca-certificates/cacert.pem" ]; then
  export CURL_CA_BUNDLE="$ZEROBREW_PREFIX/share/ca-certificates/cacert.pem"
  export SSL_CERT_FILE="$ZEROBREW_PREFIX/share/ca-certificates/cacert.pem"
fi

if [ -d "$ZEROBREW_PREFIX/etc/ca-certificates" ]; then
  export SSL_CERT_DIR="$ZEROBREW_PREFIX/etc/ca-certificates"
elif [ -d "$ZEROBREW_PREFIX/share/ca-certificates" ]; then
  export SSL_CERT_DIR="$ZEROBREW_PREFIX/share/ca-certificates"
fi

# Helper function to safely append to PATH
_zb_path_append() {
    local argpath="$1"
    case ":${PATH}:" in
        *:"$argpath":*) ;;
        *) export PATH="$argpath:$PATH" ;;
    esac;
}

_zb_path_append "$ZEROBREW_BIN"
_zb_path_append "$ZEROBREW_PREFIX/bin"
